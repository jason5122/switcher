import("//build/config/mac/templates/icon.gni")
import("//build/config/mac/templates/macos_app_bundle.gni")

macos_app_bundle("app") {
  output_name = "Switcher"
  bundle_identifier = "com.jason.switcher"
  info_plist = "resources/Info.plist"

  # define `code_signing_identity` in args.gn

  sources = [
    "AppDelegate.mm",
    "Menu.m",
    "controller/CaptureViewController.mm",
    "controller/WindowController.mm",
    "extensions/ScreenCaptureKit.m",
    "main.mm",
    "model/application.mm",
    "model/capture_engine.mm",
    "model/shortcut_manager.mm",
    "model/window.mm",
    "private_apis/CGSSpace.mm",
    "util/file_util.mm",
    "util/log_util.mm",
    "util/shader_util.cc",
    "view/CaptureView.mm",
  ]

  frameworks = [
    "Cocoa.framework",
    "OpenGL.framework",
    "CoreMedia.framework",
    "IOSurface.framework",
    "QuartzCore.framework",
    "ScreenCaptureKit.framework",
    "GLKit.framework",
    "Carbon.framework",
    "ShortcutRecorder.framework",
    "SkyLight.framework",
  ]

  framework_dirs = [
    "third_party",
    "/System/Library/PrivateFrameworks",
  ]

  include_dirs = [ "//app" ]

  defines = [ "GL_SILENCE_DEPRECATION" ]

  deps = [
    ":icon",
    ":shaders",
    ":shortcut_recorder_bundle_data",
  ]
}

icon("icon") {
  sources = [ "resources/app.iconset" ]
}

bundle_data("shaders") {
  sources = [
    "resources/shaders/texture.vsh",
    "resources/shaders/textureRect.fsh",
  ]
  outputs = [ "{{bundle_resources_dir}}/shaders/{{source_file_part}}" ]
}

bundle_data("shortcut_recorder_bundle_data") {
  sources = [ "third_party/ShortcutRecorder.framework" ]
  outputs = [ "{{bundle_contents_dir}}/Frameworks/{{source_file_part}}" ]
}
