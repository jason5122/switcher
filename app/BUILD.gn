import("//build/config/mac/templates/icon.gni")
import("//build/config/mac/templates/macos_app_bundle.gni")

macos_app_bundle("app") {
  output_name = "ObjCppApp"
  bundle_identifier = "com.jason.obj-cpp-app"
  info_plist = "resources/Info.plist"
  code_signing_identity = "34C6CC573389B86AB2388388E5091CBD104B8FF2"

  sources = [
    "AppDelegate.h",
    "AppDelegate.m",
    "BoingRenderer.mm",
    "CaptureEngine.mm",
    "FileUtil.mm",
    "Menu.m",
    "OpenGLView.mm",
    "ViewController.h",
    "ViewController.mm",
    "WindowController.m",
    "main.m",
  ]

  frameworks = [
    "Cocoa.framework",
    "OpenGL.framework",
    "QuartzCore.framework",
    "ScreenCaptureKit.framework",
  ]

  include_dirs = [ "third_party" ]
  defines = [ "GL_SILENCE_DEPRECATION" ]

  deps = [
    ":fonts",
    ":icon",
    ":images",
    ":shaders",
  ]
}

icon("icon") {
  sources = [
    "resources/app.iconset",
    "resources/document.iconset",
  ]
}

bundle_data("images") {
  sources = [
    "resources/images/awesomeface.png",
    "resources/images/container.jpg",
    "resources/images/tux.png",
  ]
  outputs = [ "{{bundle_resources_dir}}/images/{{source_file_part}}" ]
}

bundle_data("fonts") {
  sources = [
    "resources/fonts/SourceCodePro-Bold.ttf",
    "resources/fonts/SourceCodePro-Italic.ttf",
    "resources/fonts/SourceCodePro-Regular.ttf",
  ]
  outputs = [ "{{bundle_resources_dir}}/fonts/{{source_file_part}}" ]
}

bundle_data("shaders") {
  sources = [
    "resources/shaders/coordinate_system.fs",
    "resources/shaders/coordinate_system.vs",
    "resources/shaders/text.fs",
    "resources/shaders/text.vs",
  ]
  outputs = [ "{{bundle_resources_dir}}/shaders/{{source_file_part}}" ]
}
