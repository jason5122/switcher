import("//build/config/mac/templates/icon.gni")
import("//build/config/mac/templates/macos_app_bundle.gni")

macos_app_bundle("app") {
  output_name = "Switcher"
  bundle_identifier = "com.jason.switcher"
  info_plist = "resources/Info.plist"

  # define `code_signing_identity` in args.gn

  sources = [
    "AppDelegate.mm",
    "controller/CaptureViewController.mm",
    "controller/WindowController.mm",
    "controller/shortcut_controller.mm",
    "extensions/NSImage+InitWithId.mm",
    "extensions/NSWindow+ActuallyCenter.m",
    "extensions/ScreenCaptureKit+InitWithId.m",
    "main.mm",
    "model/application.mm",
    "model/applications.mm",
    "model/space.mm",
    "model/window_element.mm",
    "util/log_util.mm",
    "util/shader_util.cc",
    "view/CACaptureView.mm",
    "view/CaptureView.mm",
    "view/MainView.mm",
  ]

  frameworks = [
    "Cocoa.framework",
    "OpenGL.framework",
    "CoreMedia.framework",
    "IOSurface.framework",
    "QuartzCore.framework",
    "ScreenCaptureKit.framework",
    "GLKit.framework",
    "Carbon.framework",
    "SkyLight.framework",
  ]

  framework_dirs = [ "/System/Library/PrivateFrameworks" ]

  include_dirs = [
    "//app",
    "//app/third_party/shortcut_recorder/src",
    "$root_gen_dir/app",  # generated Swift headers
  ]

  defines = [ "GL_SILENCE_DEPRECATION" ]

  deps = [
    ":capture_preview_swift",
    ":icon",
    "//app/third_party/shortcut_recorder:shortcut_recorder",
  ]
}

source_set("capture_preview_swift") {
  sources = [
    "model/CaptureEngine.swift",
    "util/LogUtil.swift",
    "view/CapturePreview.swift",
  ]
  bridge_header = "view/capture_preview_swift_bridge.h"
}

icon("icon") {
  sources = [ "resources/app.iconset" ]
}
