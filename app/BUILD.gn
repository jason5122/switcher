import("//build/config/mac/templates/icon.gni")
import("//build/config/mac/templates/macos_app_bundle.gni")

macos_app_bundle("app") {
  output_name = "Switcher"
  bundle_identifier = "com.jason5122.switcher"
  info_plist = "resources/Info.plist"

  # define `code_signing_identity` in args.gn

  sources = [
    "AppDelegate.mm",
    "controller/CaptureViewController.mm",
    "controller/WindowController.mm",
    "controller/shortcut_controller.mm",
    "extensions/NSImage+InitWithId.mm",
    "extensions/NSWindow+ActuallyCenter.m",
    "extensions/ScreenCaptureKit+InitWithId.m",
    "main.mm",
    "model/application.mm",
    "model/applications.mm",
    "model/space.mm",
    "model/window_element.mm",
    "util/shader_util.cc",
    "view/CaptureView.mm",
    "view/GLCaptureView.mm",
    "view/MainView.mm",
    "view/TimerView.mm",
  ]

  frameworks = [
    "Cocoa.framework",
    "OpenGL.framework",
    "CoreMedia.framework",
    "IOSurface.framework",
    "QuartzCore.framework",
    "ScreenCaptureKit.framework",
    "GLKit.framework",
    "Carbon.framework",
    "SkyLight.framework",
  ]

  framework_dirs = [ "/System/Library/PrivateFrameworks" ]

  include_dirs = [
    "//app",
    "//app/third_party/shortcut_recorder/src",
    "$root_gen_dir/app",  # generated Swift headers
  ]

  defines = [ "GL_SILENCE_DEPRECATION" ]

  deps = [
    ":icon",
    ":log_util",
    ":swift_capture_view",
    "//app/third_party/shortcut_recorder:shortcut_recorder",
  ]
}

source_set("swift_capture_view") {
  sources = [
    "util/LogUtil.swift",
    "view/SwiftCaptureView.swift",
  ]
  bridge_header = "view/swift_capture_view_bridge.h"

  include_dirs = [ "//app" ]

  deps = [ ":log_util" ]
}

source_set("log_util") {
  sources = [ "util/log_util.mm" ]
}

icon("icon") {
  sources = [ "resources/app.iconset" ]
}
