import("//build/config/mac/templates/icon.gni")
import("//build/config/mac/templates/macos_app_bundle.gni")

macos_app_bundle("app") {
  output_name = "Switcher"
  bundle_identifier = "com.jason.switcher"
  info_plist = "resources/Info.plist"

  # define `code_signing_identity` in args.gn

  sources = [
    "AppDelegate.mm",
    "controller/CaptureViewController.mm",
    "controller/WindowController.mm",
    "controller/shortcut_controller.mm",
    "extensions/NSImage+InitWithId.mm",
    "extensions/NSWindow+ActuallyCenter.m",
    "extensions/ScreenCaptureKit+InitWithId.m",
    "main.mm",
    "model/application.mm",
    "model/applications.mm",
    "model/space.mm",
    "model/window_element.mm",
    "util/file_util.mm",
    "util/log_util.mm",
    "util/shader_util.cc",
    "view/CACaptureView.mm",
    "view/CaptureView.mm",
    "view/MainView.mm",
  ]

  frameworks = [
    "Cocoa.framework",
    "OpenGL.framework",
    "CoreMedia.framework",
    "IOSurface.framework",
    "QuartzCore.framework",
    "ScreenCaptureKit.framework",
    "GLKit.framework",
    "Carbon.framework",
    "ShortcutRecorder.framework",
    "SkyLight.framework",
  ]

  framework_dirs = [
    "third_party",
    "/System/Library/PrivateFrameworks",
  ]

  include_dirs = [
    "//app",
    "$root_gen_dir/app",
  ]

  defines = [ "GL_SILENCE_DEPRECATION" ]

  deps = [
    ":capture_preview_swift",
    ":icon",
    ":shaders",
    ":shortcut_recorder_bundle_data",
  ]
}

source_set("capture_preview_swift") {
  sources = [
    "model/CaptureEngine.swift",
    "util/LogUtil.swift",
    "view/CapturePreview.swift",
  ]
  bridge_header = "model/capture_preview_swift_bridge.h"
}

icon("icon") {
  sources = [ "resources/app.iconset" ]
}

bundle_data("shaders") {
  sources = [
    "resources/shaders/texture.vsh",
    "resources/shaders/textureRect.fsh",
  ]
  outputs = [ "{{bundle_resources_dir}}/shaders/{{source_file_part}}" ]
}

bundle_data("shortcut_recorder_bundle_data") {
  sources = [ "third_party/ShortcutRecorder.framework" ]
  outputs = [ "{{bundle_contents_dir}}/Frameworks/{{source_file_part}}" ]
}
