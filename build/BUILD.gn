import("//build/config/mac/mac_sdk.gni")

config("compiler") {
  configs = [
    ":include_dirs",
    ":cpp_standard",
    ":objc_standard",
    ":objc_use_arc",
    ":objc_abi_version",
    ":mac_minimum_version",
  ]
}

config("shared_binary") {
  if (current_os == "ios" || current_os == "mac") {
    configs = [ ":rpath_config" ]
  }
}

config("objc_abi_version") {
  cflags_objc = [ "-fobjc-abi-version=2" ]
  cflags_objcc = cflags_objc
  ldflags = [
    "-Xlinker",
    "-objc_abi_version",
    "-Xlinker",
    "2",
  ]
}

config("include_dirs") {
  include_dirs = [
    "//",
    root_gen_dir,
  ]
}

config("objc_use_arc") {
  cflags_objc = [
    "-fobjc-arc",
    "-fobjc-weak",
  ]
  cflags_objcc = cflags_objc
}

config("cpp_standard") {
  cflags_c = [ "--std=c11" ]
  cflags_cc = [
    "--std=c++17",
    "--stdlib=libc++",
  ]
  ldflags = [ "--stdlib=libc++" ]
}

config("objc_standard") {
  cflags_objc = [ "--std=c11" ]
  cflags_objcc = [
    "--std=c++17",
    "--stdlib=libc++",
  ]
  ldflags = [ "--stdlib=libc++" ]
}

config("mac_minimum_version") {
  cflags = [ "-mmacos-version-min=$mac_deployment_target" ]
  ldflags = [ "-mmacos-version-min=$mac_deployment_target" ]

  cflags += [ "--target=x86_64-apple-macos" ]
  ldflags += [ "--target=x86_64-apple-macos" ]

  # TODO: Don't hard code to arm64.
  # TODO: Fix "built for newer 'macOS' version" warnings when deploying for <13.0.
  # swiftflags = [ "-target=arm64-apple-macos13.0" ]
  swiftflags = [ "-target=x86_64-apple-macos13.0" ]
}

if (current_os == "ios" || current_os == "mac") {
  config("rpath_config") {
    ldflags = [
      "-Xlinker",
      "-rpath",
      "-Xlinker",
      "@executable_path/../Frameworks",
      "-Xlinker",
      "-rpath",
      "-Xlinker",
      "@loader_path/../Frameworks",
    ]
  }
}
